# Homebrew PATH - must be first
{{ if eq .chezmoi.os "darwin" -}}
{{ if eq .chezmoi.arch "arm64" -}}
fish_add_path /opt/homebrew/bin
{{- else -}}
fish_add_path /usr/local/bin
{{- end }}
{{- else -}}
fish_add_path /home/linuxbrew/.linuxbrew/bin
{{- end }}

# Local bin
fish_add_path $HOME/.local/bin

# OS-specific paths
{{ if eq .chezmoi.os "linux" -}}
fish_add_path /opt/nvim/bin
{{- end }}

{{ if stat (joinPath .chezmoi.homeDir ".opencode/bin") -}}
fish_add_path {{ .chezmoi.homeDir }}/.opencode/bin
{{- end }}

if status is-interactive
    # Commands to run in interactive sessions can go here
end

# Inform kitty of the current working directory (for tab titles)
function _kitty_osc --on-event fish_prompt
    printf '\e]7;file://%s%s\e\\' (hostname) (pwd | string escape --style=url)
end

set fish_greeting ""
set -g fish_key_bindings fish_vi_key_bindings

# NPM Token - only load if file exists (NOT in repo)
if test -f ~/.npm_token
    set -gx NPM_TOKEN (string trim (cat ~/.npm_token))
end

# Initialize tools (only if they exist)
if type -q zoxide
    zoxide init fish | source
end

if type -q starship
    starship init fish | source
end

if type -q mise
    mise activate fish | source
end

# Aliases
alias lg="lazygit"
alias y="yazi"

# Conditional aliases - only if tools exist
if type -q feeds
    alias fstart="feeds start"
    alias fstop="feeds stop"
    alias fup="feeds up"
    alias fdown="feeds down"
end

if type -q creatives
    alias cstart="creatives start"
    alias cstop="creatives stop"
    alias cup="creatives up"
    alias cdown="creatives down"
end

# Atuin - shell history (only if installed)
if type -q atuin
    set -gx ATUIN_SESSION (atuin uuid)
    set --erase ATUIN_HISTORY_ID

    function _atuin_preexec --on-event fish_preexec
        if not test -n "$fish_private_mode"
            set -g ATUIN_HISTORY_ID (atuin history start -- "$argv[1]")
        end
    end

    function _atuin_postexec --on-event fish_postexec
        set -l s $status

        if test -n "$ATUIN_HISTORY_ID"
            ATUIN_LOG=error atuin history end --exit $s -- $ATUIN_HISTORY_ID &>/dev/null &
            disown
        end

        set --erase ATUIN_HISTORY_ID
    end

    function _atuin_search
        set -l keymap_mode
        switch $fish_key_bindings
            case fish_vi_key_bindings
                switch $fish_bind_mode
                    case default
                        set keymap_mode vim-normal
                    case insert
                        set keymap_mode vim-insert
                end
            case '*'
                set keymap_mode emacs
        end

        set -l ATUIN_H (ATUIN_SHELL_FISH=t ATUIN_LOG=error ATUIN_QUERY=(commandline -b) atuin search --keymap-mode=$keymap_mode $argv -i 3>&1 1>&2 2>&3 | string collect)

        if test -n "$ATUIN_H"
            if string match --quiet '__atuin_accept__:*' "$ATUIN_H"
              set -l ATUIN_HIST (string replace "__atuin_accept__:" "" -- "$ATUIN_H" | string collect)
              commandline -r "$ATUIN_HIST"
              commandline -f repaint
              commandline -f execute
              return
            else if string match --quiet '__atuin_chain_command__:*' "$ATUIN_H"
              set -l new_command (string replace "__atuin_chain_command__:" "" -- "$ATUIN_H" | string collect)
              set -l current_command (commandline -b)
              commandline -r "$current_command $new_command"
            else
              commandline -r "$ATUIN_H"
            end
        end

        commandline -f repaint
    end

    function _atuin_bind_up
        if commandline --search-mode; or commandline --paging-mode
            up-or-search
            return
        end

        set -l lineno (commandline --line)

        switch $lineno
            case 1
                _atuin_search --shell-up-key-binding
            case '*'
                up-or-search
        end
    end

    bind \cf _atuin_search
    bind \eOA _atuin_bind_up
    bind \e\[A _atuin_bind_up

    if bind -M insert > /dev/null 2>&1
        bind -M insert \cr _atuin_search
        bind -M insert \eOA _atuin_bind_up
        bind -M insert \e\[A _atuin_bind_up
    end
end
